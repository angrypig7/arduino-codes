# Modular Eagle Setup Scripts
# 
# File: MENU.SCR
# Description: Context and user menu setup.
# Author: Cameron Nicks, PCB Designer
# 
#
#   To run by default, include this file in eagle.scr otherwise use SCRIPT to run
#--------------------------------------------------------------


#----------------------------------#
#-------------- NOTES -------------#
#----------------------------------#
# Notes about CONTEXT MENU
#attribute, circle, dimension, element, frame, gate, hole, instance, junction, label, modinst (module instance), pad, pin, rectangle, smd, text, via and wire.

#Restriction note on ULP execution:
#In order to have the selected object available for function ingroup() please avoid setting the ';' within the apostrophs (i.e. not 'run myexport.ulp;', but 'run myexport.ulp').

#To-Do: Figure out if there is a way to swap or change icons on the fly. (ie, after CFGTOGGLE to change icon to reflect current setting)
#______________________________________________________________

#----------------------------------#
#---------- BOARD EDITOR ----------#
#----------------------------------#
BRD:
	# BOARD text command menu setup
	 
#	SET CONTEXT; #This clears all user context menu items. Disabled so --install options do not get over written
	SET CONTEXT ELEMENT;
	SET CONTEXT ELEMENT  	'ROT - 45'			'RUN GRP-ROTATE 45 -';
	SET CONTEXT ELEMENT  	'ROT - 90'			'RUN GRP-ROTATE 90 -';
	SET CONTEXT ELEMENT  	'ROT - 180'			'RUN GRP-ROTATE 180 -';
	SET CONTEXT ELEMENT  	'Show Part Airwires -GND'	'RUN SHOW-BRD-PART-NETS GND';
	SET CONTEXT ELEMENT  	'Display Pad Names'			'RUN CFGTOGGLE Option.PadNames';
	SET CONTEXT ELEMENT		'View Datasheet' 'RUN getDatasheet';
	
#	SET CONTEXT WIRE		'Length?'	'RUN length'; #Length needs modified to run from context.
	SET CONTEXT WIRE		'Display Signal Names'	'RUN CFGTOGGLE Option.SignalNamesOnTraces';
	SET CONTEXT WIRE  		'ROT - 45'			'RUN GRP-ROTATE 45 -';
	SET CONTEXT WIRE  		'ROT - 90'			'RUN GRP-ROTATE 90 -';
	SET CONTEXT WIRE  		'ROT - 180'			'RUN GRP-ROTATE 180 -';
	
	SET CONTEXT TEXT  		'ROT - 45'			'RUN GRP-ROTATE 45 -';
	SET CONTEXT TEXT  		'ROT - 90'			'RUN GRP-ROTATE 90 -';
	SET CONTEXT TEXT  		'ROT - 180'			'RUN GRP-ROTATE 180 -';
	
#	SET CONTEXT ELEMENT		'Move by Displacement'	'RUN MOVEBY'; #This has been replaced with RUN MOVEBY --install

#MENU SHOW Doesn't handle 2 digit des, need to make list in ulp to call from
	MENU
		'Step Layer {\
			UP       	: RUN trace-layer -; |\
			DOWN 		: RUN trace-layer +; |\
				}'\
		'SELECT : RUN SELECT;'\
		'SHOW {\
			ELEMENTS {\
				ALL		:	SHOW A? C* D? F? J? Q? R? S? U?|\
				C...	: 	SHOW C*|\
				R...	: 	SHOW R*|\
				L...	: 	SHOW L*|\
				U...	: 	SHOW U*|\
				J...	: 	SHOW J*|\
				D...	: 	SHOW D*|\
				Q...	: 	SHOW Q*|\
			}|\
			Show Class		: RUN showclass; |\
			GRP in SCH : RUN SHOW-GROUP-PARTS; |\
			PART - Nets : RUN SHOW-BRD-PART-NETS GND; |\
			Show Values : RUN SHOW_VALUES; |\
		}'\
		'RATS 	{\
			All			: SCRIPT Rats-All.scr; |\
			None		: SCRIPT Rats-None.scr; |\
			Hide Pwr	: SCRIPT Rats-Pwr-Hide.scr; |\
			Show Pwr	: SCRIPT Rats-Pwr-Show.scr; |\
		}'\
		'POUR 	{\
			All			: SCRIPT Pour-All.scr; |\
			V* Nets		: SCRIPT Pour-V.scr; |\
			Clr GND		: SCRIPT Pour-CLR-GND.scr; |\
		}'\
		'POLYGON {\
			Solid/Outline 	:	RUN CFGTOGGLE Option.DrawUnprocessedPolygonEdgesContinuous;|\			
			Process 		:	RUN CFGTOGGLE Option.PolygonRatsnest; |\
		}'\
     '---'\
		'OPTIONS {\
			Set Used Layers	:	RUN UseLayers;|\
			Display {\
				Pad Names 		:	RUN CFGTOGGLE Option.PadNames;|\
				Signals on Pad	: 	RUN CFGTOGGLE Option.SignalNamesOnPads;|\
				Signals on Trace:	RUN CFGTOGGLE Option.SignalNamesOnTraces;|\
				Text Origins 	:	RUN CFGTOGGLE Option.ShowTextOrigins;|\
			}|\
			RENDER {\
				Top       : SCRIPT RenderT.scr; |\
				Bottom : SCRIPT RenderB.scr; |\
			}\
		}'\
		'UTILITY {\
			Live DRC : RUN CFGTOGGLE Option.LiveDrc;; |\
			ATTRIBUTES {\
				COPY				: RUN ATT-COPY; |\
				VIEW LIST		: RUN attview; |\
			}|\
			EXPORT {\
				BOM					: edit .sch; RUN BOM-EX; |\
				XY File (SMD) : RUN XYSMD; |\
				XY File (TH) : RUN XYTH; |\
			}|\
			DOC {\
				DRILLEGEND			: RUN DRL; RUN Drills; |\
				PRINT PDF		:	DISPLAY PDF; PRINT 1 -1 FILE %P/../%N.%E.pdf |\
			}|\
			MOUNT	{\
				Usage			: RUN mount-trace; |\
				Build List : RUN mount-trace; |\
				Show Next : RUN mount-trace +; |\
				Show Prev : RUN mount-trace -; |\
				Info Next :	RUN mount-trace ++; |\
				Info Prev : RUN mount-trace --; |\
			}|\
			PANELIZE {\
				TEXT			: RUN PANELIZE-TEXTMM; |\
				RENAME {\
					PARTS   : RUN PART-SUFFIX; |\
					SIGNALS	: RUN rename-signal; |\
				}|\
			}|\
			SETTINGS {\
				Export Layer Styles : RUN board-layer-styles; |\
			}|\
			TEARDROP {\
				[C:\LAYOUT\Library\ULP\BRD\TEARDROP\tear_exec30.png]	ADD			: RUN TEARDROP; |\
				[C:\LAYOUT\Library\ULP\BRD\TEARDROP\de_tear30.png] REMOVE		: RUN CLEANWIRE; |\
			}|\
		}'\	
				
				#TO-DO:
				#Statistics section.
				#Finishing Processes. Teardrop, DRL, Drills.

#----------------------------------#
#------- SCHEMATIC EDITOR ---------#
#----------------------------------#				
SCH:
	# Schematic text command menu setup
	
#	SET CONTEXT; # These are not working for some reason. Object type? Yes, Element -> Instance shows up

	SET CONTEXT WIRE		'Show in Brd'	'RUN SHOW-SCH-GROUP';
	SET CONTEXT WIRE		'Show Airwires'	'RUN SHOW-SCH-GROUP -rats';
	
	SET CONTEXT INSTANCE	'Move by Displacement'	'RUN MOVEBY';
	SET CONTEXT INSTANCE	'Show in Brd'	'RUN SHOW-SCH-GROUP';
	SET CONTEXT INSTANCE	'Show Airwires'	'RUN SHOW-SCH-GROUP -rats'; #This doesn't work to show group and only focuses on the single part right clicked on. Need context meu in blank space that can activate a whoel group.
	SET CONTEXT INSTANCE	'View Datasheet' 'RUN getDatasheet';
	
		MENU 
		'GROUP {\
			BRD Transfer Group			: RUN select-sch-group-to-brd-group; |\
			BRD Show Group : RUN SHOW-SCH-GROUP; |\
			BRD Show - Solo Nets : RUN SHOW-SCH-GROUP -rats; |\
				}'\
		'SHOW {\
			Highlight Class		:	RUN showclass; |\
			Highlight Values : RUN SHOW_VALUES; |\
			Display Part Origins	:	RUN CFGTOGGLE Option.ShowPartOrigins; |\
			Display Text Origins 	:	RUN CFGTOGGLE Option.ShowTextOrigins; |\
		}'\
		'CHANGE {\
			Line Width	: CHANGE WIDTH |\
			Text Size		: CHANGE SIZE |\
			Text Align	:	CHANGE ALIGN |\
			Package			: CHANGE PACKAGE |\
			Technology	: CHANGE TECH |\
			Component		: REPLACE |\			
			Net Class		: CHANGE CLASS |\
				}'\
		'UTIL {\
			Print PDF		:	PRINT SHEETS ALL FILE %P/../%N.%E.pdf |\
			BOM					: RUN BOM-EX; |\
			SELECT					: RUN SELECT; |\
		}'\	

#----------------------------------#
#--------- LIBRARY EDITOR ---------#
#----------------------------------#
LBR:
	# Library text command menu setup

	MENU
		'[c:/LAYOUT/Library/ULP/icons/ipc_wizard.svg] IPC-Wizard : RUN ipc-wizard;'

DEV:
	# Device text command menu setup
	MENU
		'' |\
		'ATTRIBUTE {\
			Copy to All Tech/Var		: RUN attribute-copy-technologies; |\
			Set - STANDARD : SCRIPT ATT_SET_STANDARD.scr; |\
			Set - ACTIVE : SCRIPT ATT_SET_ACTIVE.scr; |\
			Set - CAP : SCRIPT ATT_SET_CAP.scr; |\
			Set - CONN : SCRIPT ATT_SET_CONN.scr; |\
			Set - DIODE : SCRIPT ATT_SET_DIODE.scr; |\
			Set - IND : SCRIPT ATT_SET_IND.scr; |\
			Set - REG : SCRIPT ATT_SET_REG.scr; |\
			Set - RES : SCRIPT ATT_SET_RES.scr; |\
			Set - VENDOR : SCRIPT ATT_SET_VENDOR.scr; |\
			INDIVIDUAL {\
				BOM				: run attribute-deviceset BOM; |\
				EXCLUDE				: run attribute-deviceset EXCLUDE; |\
				EXTERNAL				: run attribute-deviceset _EXTERNAL_; |\
			}|\
		}'\	


PAC:
	# Package text command menu setup

	MENU
		'CMD Draw : RUN cmd-draw;' |\
		'IPC-Wizard : RUN ipc-wizard;' |\
		'LibIt! : RUN LibIt;' |\
		'MoveBy : RUN MOVEBY;' |\
		'NAME/VAL : SCRIPT namevalue.scr;' |\
		'Rotate/Scale : RUN rotate-scale;'
		
	SET CONTEXT;
	SET CONTEXT ELEMENT		'Move by Displacement'	'RUN MOVEBY';

SYM:
	# Symbol text command menu setup

	MENU
		'CMD Draw : RUN cmd-draw;' |\
		'MoveBy : RUN MOVEBY;' |\
		'NAME/VAL : SCRIPT namevalue.scr;' |\
		'Rotate/Scale : RUN rotate-scale;'
		
	SET CONTEXT;
	SET CONTEXT ELEMENT		'Move by Displacement'	'RUN MOVEBY';
	